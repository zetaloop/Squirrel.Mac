template("xcrun_action") {
  assert(defined(invoker.cmd), "Need cmd name to run")
  assert(defined(invoker.args), "Need cmd argumets")
  assert(defined(invoker.inputs), "Need inputs")
  assert(defined(invoker.outputs), "Need outputs")

  action(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "public_deps",
                             "inputs",
                             "outputs",
                           ])
    script = "//third_party/squirrel.mac/build/xcrun.py"
    args = [ invoker.cmd ] + invoker.args
  }
}
